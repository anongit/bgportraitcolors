ACTION_MATCH ~%WEIDU_OS%~
WITH
  ~win32~ BEGIN
    ACTION_IF FILE_EXISTS "Baldur.exe" BEGIN
      OUTER_SPRINT game_exe "Baldur.exe"
      ACTION_DEFINE_ASSOCIATIVE_ARRAY cmarker_color_offsets BEGIN
        46c1b3bc2710af125525042d6d3c6eab => 0x3c0736 // BG1:EE 2.6.6.0
        6439dc555d7b5b91c2aa59bb0f32b271 => 0x3c0736 // BG2:EE 2.6.6.0
      END
    END

    ELSE ACTION_IF FILE_EXISTS "icewind.exe" BEGIN
      OUTER_SPRINT game_exe "icewind.exe"
      ACTION_DEFINE_ASSOCIATIVE_ARRAY cmarker_color_offsets BEGIN
        52b3e6ab1f51acdff86ad1c86d113613 => 0x3c0776 // IWD:EE 2.6.6.0
      END
    END

    ELSE ACTION_IF FILE_EXISTS "Torment.exe" BEGIN
      OUTER_SPRINT game_exe "Torment.exe"
      ACTION_DEFINE_ASSOCIATIVE_ARRAY cmarker_color_offsets BEGIN
        35ab06a93a6fca57b5b5908d7a048b99 => 0x399a4f // PST:EE 3.1.4
      END
    END

    ELSE ACTION_IF FILE_EXISTS "SiegeOfDragonspear.exe"
    BEGIN OUTER_SPRINT game_exe "SiegeOfDragonspear.exe" END

    // Original games
    ELSE ACTION_IF FILE_EXISTS "bgmain.exe" // BG2
    BEGIN OUTER_SPRINT game_exe "bgmain.exe" END

    ELSE ACTION_IF FILE_EXISTS "bgmain2.exe" // ToSC
    BEGIN OUTER_SPRINT game_exe "bgmain2.exe" END

    ELSE ACTION_IF FILE_EXISTS "idmain.exe" // IWD
    BEGIN OUTER_SPRINT game_exe "idmain.exe" END

    ELSE ACTION_IF FILE_EXISTS "iwd2.exe" // IWD2
    BEGIN OUTER_SPRINT game_exe "iwd2.exe" END
  END

  ~unix~ BEGIN
    ACTION_IF FILE_EXISTS "BaldursGate" BEGIN
      OUTER_SPRINT game_exe "BaldursGate"
      ACTION_DEFINE_ASSOCIATIVE_ARRAY cmarker_color_offsets BEGIN
        3cbb4dcb0c82d59eefff9078e05fd1e2 => 0x435615 // BG1:EE 2.6.6.0
      END
    END

    ELSE ACTION_IF FILE_EXISTS "BaldursGateII" BEGIN
      OUTER_SPRINT game_exe "BaldursGateII"
      ACTION_DEFINE_ASSOCIATIVE_ARRAY cmarker_color_offsets BEGIN
        3cbb4dcb0c82d59eefff9078e05fd1e2 => 0x435615 // BG2:EE 2.6.6.0
      END
    END

    ELSE ACTION_IF FILE_EXISTS "BaldursGate64" BEGIN
      OUTER_SPRINT game_exe "BaldursGate64"
      ACTION_DEFINE_ASSOCIATIVE_ARRAY cmarker_color_offsets BEGIN
        1ef0649f0177147430d09da2a342ef11 => 0x5c1b80 // BG1:EE 2.5-23121
      END
    END

    ELSE ACTION_IF FILE_EXISTS "BaldursGateII64" BEGIN
      OUTER_SPRINT game_exe "BaldursGateII64"
      ACTION_DEFINE_ASSOCIATIVE_ARRAY cmarker_color_offsets BEGIN
        1aa025187c19524641cb13391d5c14a2 => 0x5c06a0 // BG2:EE 2.5-21851
      END
    END

    ELSE ACTION_IF FILE_EXISTS "Torment64" BEGIN
      OUTER_SPRINT game_exe "Torment64"

      ACTION_DEFINE_ASSOCIATIVE_ARRAY border_color_offsets BEGIN
        74b007f6ceb6530639a329f3e59e71cf => 0xad0530 // PST:EE 3.1.4-26532
      END

      ACTION_DEFINE_ASSOCIATIVE_ARRAY cmarker_color_offsets BEGIN
        74b007f6ceb6530639a329f3e59e71cf => 0x671c33 // PST:EE 3.1.4-26532
      END
    END

    ELSE ACTION_IF FILE_EXISTS "IcewindDale" BEGIN
      OUTER_SPRINT game_exe "IcewindDale"
      ACTION_DEFINE_ASSOCIATIVE_ARRAY cmarker_color_offsets BEGIN
        3cbb4dcb0c82d59eefff9078e05fd1e2 => 0x435615 // IWD:EE 2.6.6.0
      END
    END
  END

  ~osx~ BEGIN
    COPY - GLOB ~*.app/Contents/MacOS/*-macOS~ ~%MOD_FOLDER%/tmp~
    ACTION_IF VARIABLE_IS_SET SOURCE_FILESPEC BEGIN
      OUTER_TEXT_SPRINT game_exe ~%SOURCE_FILESPEC%~
      ACTION_DEFINE_ASSOCIATIVE_ARRAY cmarker_color_offsets BEGIN
        a3d6153fef77369c64fe410b1bfaad63 => 0x3b0f1d // BG1/2:EE 2.6.6.0
      END
    END
  END

  DEFAULT ABORT ~unsupported os '%WEIDU_OS%'~
END

ACTION_IF NOT VARIABLE_IS_SET game_exe BEGIN
  ABORT ~unsupported game~
END

ACTION_PHP_EACH border_color_offsets AS md5sum => offset BEGIN
  ACTION_IF FILE_MD5 $game_exe() $md5sum() BEGIN
    OUTER_SET border_color_offset = offset
  END
END

ACTION_PHP_EACH cmarker_color_offsets AS md5sum => offset BEGIN
  ACTION_IF FILE_MD5 $game_exe() $md5sum() BEGIN
    OUTER_SET cmarker_color_offset = offset
  END
END

PRINT ~Target file: '%game_exe%'~
